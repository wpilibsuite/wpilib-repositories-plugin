plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'groovy'
    id "com.gradle.plugin-publish" version "1.2.1"
}

version = '2024.0'
group = 'edu.wpi.first.wpilib.repositories'

if (project.hasProperty('publishVersion')) {
    version = project.publishVersion
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(Javadoc) {
    options.addBooleanOption('Xdoclint:all,-missing', true)
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

gradlePlugin {
    website = 'https://github.com/wpilibsuite/wpilib-repositories-plugin'
    vcsUrl = 'https://github.com/wpilibsuite/wpilib-repositories-plugin'

    plugins {
        WpilibTools {
            id = 'edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin'
            implementationClass = 'edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin'
            displayName = 'WPILib Repositories Plugin'
            description = 'This plugin provides maven remote settings for the WPILib Suite FRC projects.'
            tags = ['maven', 'frc', 'wpilib', 'repositories']
        }
    }
}

wrapper {
    gradleVersion = '8.5'
}
